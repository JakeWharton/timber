apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 28,

      'kotlin': '1.3.21',

      'androidPlugin': '3.4.0',
      'androidTools': '26.4.0',

      'butterknife': '8.8.1',
  ]

  ext.deps = [
      androidPlugin: "com.android.tools.build:gradle:${versions.androidPlugin}",
      'kotlin': [
          'plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      ],
      'butterknife': [
          'runtime': "com.jakewharton:butterknife:${versions.butterknife}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.butterknife}"
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      annotations: 'org.jetbrains:annotations:19.0.0',

      junit: 'junit:junit:4.12',
      truth: 'com.google.truth:truth:0.41',
      robolectric: 'org.robolectric:robolectric:3.8',
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    jcenter()
  }

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlin.plugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'me.champeau.gradle:japicmp-gradle-plugin:0.2.6'
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }
}
