apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 28,

      'kotlin': '1.3.72',

      'androidPlugin': '4.0.1',
      'androidTools': '27.0.1',

      'annotations': '19.0.0',
      'butterknife': '8.8.1',
      'japicmpPlugin': '0.2.6',
      'versionsPlugin': '0.17.0',

      'junit': '4.13',
      'roboelectric': '3.8',
      'truth': '1.0.1'
  ]

  ext.deps = [
      'androidPlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",
      'japicmpPlugin': "me.champeau.gradle:japicmp-gradle-plugin:${versions.japicmpPlugin}",
      'versionsPlugin': "com.github.ben-manes:gradle-versions-plugin:${versions.versionsPlugin}",
      'kotlin': [
          'plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
      ],
      'butterknife': [
          'runtime': "com.jakewharton:butterknife:${versions.butterknife}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.butterknife}"
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      'annotations': "org.jetbrains:annotations:${versions.annotations}",

      'junit': "junit:junit:${versions.junit}",
      'truth': "com.google.truth:truth:${versions.truth}",
      'robolectric': "org.robolectric:robolectric:${versions.roboelectric}",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    jcenter()
  }

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlin.plugin
    classpath deps.versionsPlugin
    classpath deps.japicmpPlugin
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }
}
