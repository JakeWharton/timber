apply plugin: 'com.android.library'
apply plugin: 'com.kageiit.lintrules'
apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
  configFile rootProject.file('checkstyle.xml')
  source 'src/main/java'
  ignoreFailures false
  showViolations true
  include '**/*.java'

  classpath = files()
}

afterEvaluate {
  if (project.tasks.findByName('check')) {
    check.dependsOn('checkstyle')
  }
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    consumerProguardFiles 'consumer-proguard-rules.pro'
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
}

dependencies {
  provided 'org.jetbrains:annotations:15.0'

  testCompile deps.festandroid
  testCompile deps.festassert
  testCompile deps.junit
  testCompile deps.robolectric

  lintRules project(':timber-lint')
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
